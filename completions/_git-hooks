#compdef git-hooks

# Define the directory where your hook files are stored
CLONE_DIR="${GIT_HOOKS_INSTALL:-$HOME/.local/share/git-hooks/}"

__git-hooks-activate() {
    local cur
    cur=${words[CURRENT]}

    # Generate completions from the list of hook files
    _arguments \
    '1: :(pre-commit commit-msg post-commit pre-push)' \
    '*:: :__git-script-list'
}

__git-contrib-hooks() {
    local cur
    cur=${words[CURRENT]}

    # Generate completions from the list of hook files
    _arguments '1: :__contrib_hooks'
}

__git-hooks-types() {
    local cur
    cur=${words[CURRENT]}

    # Generate completions from the list of hook files
    _arguments \
    '1: :(pre-commit commit-msg post-commit pre-push)'
}

__contrib_hooks() {
    local hooks

    # find $CLONE_DIR -name '.*' -prune -o -type f -print | sed "s|$CLONE_DIR||"
    hooks=(${${(f)"$(_call_program contrib_hooks find $CLONE_DIR -name '.gitignore' -prune -o -name '.*' -prune -o -type f -print | sed "s|^$CLONE_DIR/||" 2>/dev/null)"}})

    # Generate completions from the list of hook files
    _describe 'contrib hooks' hooks
}

__git-script-list() {
    local cur
    cur=${words[CURRENT]}

    # List hook files in the hooks directory
    local hooks

    hooks=(${${(f)"$(_call_program hooks git rev-parse --show-toplevel | xargs -I {} ls "{}/.githooks/$words[1]" 2>/dev/null)"}})

    # Generate completions from the list of hook files
    _describe -t files 'hooks files' hooks

}

_git-hooks ()
{
	local curcontext="$curcontext" state line
	typeset -A opt_args

	_arguments -C \
		':command:->command' \
		'*::options:->options'

	case $state in
		(command)

			local -a subcommands
			subcommands=(
				'activate:Activate one of the available hooks.'
				'deactivate:Deactivate an activated hook.'
				'setup:Create the hooks folder structure and edit .git/hooks.'
				'list:List all available hooks'
				'list-active:List all active hooks'
                'run:Run activated hooks for a given trigger (pre-commit, commit-msg, post-commit, pre-push)'
                'arrange:Change the order of hooks for a particular trigger'
                'use: Use a contributed hook by copying to current repo'
			)
			_describe -t commands 'git hooks' subcommands
		;;

		(options)
			case $line[1] in
                (activate)
                    __git-hooks-activate
                ;;
                (deactivate)
                    __git-hooks-activate
                ;;
                (run)
                    __git-hooks-types
                ;;
                (arrange)
                    __git-hooks-types
                ;;
                (use)
                    __git-contrib-hooks
                ;;
			esac
		;;
	esac
}

compdef _git-hooks git-hooks
